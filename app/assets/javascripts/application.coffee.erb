#= require jquery
#= require jquery_ujs
#= require bootstrap/dropdown
#= require twitter-text-1.11.0
#= require superagent-1.1.0
#= require vue-0.12.8-master

#= require init
#= require_tree ./shared
#= require_tree ./controllers

$ ->
  ac = Views[Helpers.controller()]
  if ac
    ac["_"]?()
    ac[Helpers.action()]?()

  Helpers.parts().forEach (par) ->
    (Shared[par])?()

$ ->
  vm = new Vue
    el: "#user-jump-dropdown"
    data:
      users: []
      enteredUserName: ""
      loading: false
    methods:
      failProfileImage: (e) ->
        e.target.src = '<%= image_path("profile_image_missing.png") %>'
      focus: ->
        setTimeout (-> $("#user-jump-dropdown input").focus()), 0
      submit: (e) ->
        e.preventDefault()
        window.location = "/" + this.enteredUserName

  currentReq = null
  vm.$watch "enteredUserName", (newVal, oldVal) ->
    if newVal.length > 0 && newVal != oldVal
      if vm.loading
        currentReq.abort()
      vm.users = []
      vm.loading = true
      currentReq =
        superagent
          .get "/i/api/users/suggest_screen_name?head=" + encodeURIComponent(newVal)
          .accept "json"
          .end (err, res) ->
            vm.users = res.body
            vm.loading = false
